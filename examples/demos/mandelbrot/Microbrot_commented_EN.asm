; X is column on screen
; Y is row on screen


; X=0, Y=0              X=319, Y=0
; +------------------------------->
; |
; |
; |
; |
; |
; |
; v
; X=0, Y=199            X=319, Y=199


; switch to VGA 320*200*256 graphics mode
mov al,13h
int 10h
; initial BX is 0
; initial DI is 0xFFFE
; DS:BX (or DS:0) is pointing to Program Segment Prefix at this moment
; ... first 4 bytes of which are CD 20 FF 9F
les ax,[bx]
; ES:AX=9FFF:20CD

FillLoop:
; \verb|set DX to 0. CWD works as: DX:AX = sign_extend(AX). |
; AX here 0x20CD (at startup) or less then 320 (when getting back after loop),
; so DX will always be 0.
cwd 
mov ax,di
; AX is current pointer within VGA buffer
; divide current pointer by 320
mov cx,320
div cx
; \verb|DX (start_X) - remainder (column: 0..319); AX - result (row: 0..199)|
sub ax,100
; \verb|AX=AX-100, so AX (start_Y) now is in range -100..99|
; DX is in range 0..319 or 0x0000..0x013F
dec dh
; DX now is in range 0xFF00..0x003F (-256..63)

xor bx,bx
xor si,si
; \verb|BX (temp_X)=0; SI (temp_Y)=0|

; get maximal number of iterations
; CX is still 320 here, so this is also maximal number of iteration
MandelLoop:
mov bp,si      ; \verb|BP = temp_Y|
imul si,bx     ; \verb|SI = temp_X*temp_Y|
add si,si      ; \verb|SI = SI*2 = (temp_X*temp_Y)*2|
imul bx,bx     ; \verb|BX = BX^2 = temp_X^2|
jo MandelBreak ; overflow?
imul bp,bp     ; \verb|BP = BP^2 = temp_Y^2|
jo MandelBreak ; overflow?
add bx,bp      ; \verb|BX = BX+BP = temp_X^2 + temp_Y^2|
jo MandelBreak ; overflow?
sub bx,bp      ; \verb|BX = BX-BP = temp_X^2 + temp_Y^2 - temp_Y^2 = temp_X^2|
sub bx,bp      ; \verb|BX = BX-BP = temp_X^2 - temp_Y^2|

; correct scale:
sar bx,6       ; BX=BX/64
add bx,dx      ; \verb|BX=BX+start_X|
; \verb|now temp_X = temp_X^2 - temp_Y^2 + start_X|
sar si,6       ; SI=SI/64
add si,ax      ; \verb|SI=SI+start_Y|
; \verb|now temp_Y = (temp_X*temp_Y)*2 + start_Y|

loop MandelLoop

MandelBreak:
; CX=iterations
xchg ax,cx
; AX=iterations. store AL to VGA buffer at ES:[DI]
stosb
; stosb also increments DI, so DI now points to the next point in VGA buffer
; jump always, so this is eternal loop here
jmp FillLoop
