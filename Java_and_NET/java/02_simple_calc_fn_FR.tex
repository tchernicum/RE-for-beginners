% TODO proof-reading
% French translation: I have not translated the term "TOS"
\subsection{Fonctions de calculs simples}

Continuons avec une fonction de calcul simple.

\begin{lstlisting}[style=customjava]
public class calc
{
	public static int half(int a)
	{
		return a/2;
	}
}
\end{lstlisting}

Voici la sortie quand l'instruction \INS{iconst\_2} est utilisée :


\begin{lstlisting}
  public static int half(int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=1, args_size=1
         0: iload_0       
         1: iconst_2      
         2: idiv          
         3: ireturn       
\end{lstlisting}
         
\INS{iload\_0} prend le zéroième argument de la fonction et le pousse sur la pile.

\INS{iconst\_2} pousse 2 sur la pile.
Après l'exécution de ces deux instructions, voici à quoi ressemble la pile :


% FIXME: TikZ
\begin{lstlisting}
      +---+
TOS ->| 2 |
      +---+
      | a |
      +---+
\end{lstlisting}

\INS{idiv} prend juste les deux valeurs depuis le \ac{TOS}, divise l'un par l'autre et laisse 
le résultat au \ac{TOS}:


% FIXME: TikZ
\begin{lstlisting}
      +--------+
TOS ->| result |
      +--------+
\end{lstlisting}

\INS{ireturn} le prend et le renvoie.

Procédons avec un nombre flottant double précision :


\begin{lstlisting}[style=customjava]
public class calc
{
	public static double half_double(double a)
	{
		return a/2.0;
	}
}
\end{lstlisting}

\begin{lstlisting}[caption=Constant pool]
...
   #2 = Double             2.0d
...
\end{lstlisting}

\begin{lstlisting}
  public static double half_double(double);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=2, args_size=1
         0: dload_0       
         1: ldc2_w        #2                  // double 2.0d
         4: ddiv          
         5: dreturn       
\end{lstlisting}

C'est pareil, mais l'instruction \INS{ldc2\_w} est utilisée pour charger la constante
2.0 depuis le pool de constantes.

Aussi, les trois autres instructions ont le \emph{d} en préfixe,
ce qui signifie qu'elles fonctionnent avec des valeurs de type \emph{double}.


Utilisons maintenant une fonction avec deux arguments :

\begin{lstlisting}[style=customjava]
public class calc
{
	public static int sum(int a, int b)
	{
		return a+b;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static int sum(int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=2
         0: iload_0       
         1: iload_1       
         2: iadd          
         3: ireturn       
\end{lstlisting}

\INS{iload\_0} charge le premier argument de la fonction (a), \INS{iload\_1}---le second (b).

Voici la pile après l'exécution de ces instructions :

\begin{lstlisting}
      +---+
TOS ->| b |
      +---+
      | a |
      +---+
\end{lstlisting}

\INS{iadd} ajoute les deux valeurs et laisse le résultat au \ac{TOS}:


\begin{lstlisting}
      +----------+
TOS ->| resultat |
      +----------+
\end{lstlisting}

Etendons cet exemple avec le type \emph{long} :


\begin{lstlisting}[style=customjava]
	public static long lsum(long a, long b)
	{
		return a+b;
	}
\end{lstlisting}

\dots nous avons :

\begin{lstlisting}
  public static long lsum(long, long);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=4, args_size=2
         0: lload_0       
         1: lload_2       
         2: ladd          
         3: lreturn       
\end{lstlisting}

La deuxième instruction \TT{lload} prend le second argument depuis la 2ème position.

C'est parce que la valeur d'un \emph{long} de 64-bit occupe exactement deux places de 32-bit.


Example un peu plus avancé :


\begin{lstlisting}[style=customjava]
public class calc
{
	public static int mult_add(int a, int b, int c)
	{
		return a*b+c;
	}
}
\end{lstlisting}

\begin{lstlisting}
  public static int mult_add(int, int, int);
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=3
         0: iload_0       
         1: iload_1       
         2: imul          
         3: iload_2       
         4: iadd          
         5: ireturn       
\end{lstlisting}

La première étape est la multiplication. Le produit est laissé au \ac{TOS}:


\begin{lstlisting}
      +---------+
TOS ->| produit |
      +---------+
\end{lstlisting}

\TT{iload\_2} charge le troisième argument (c) dans la pile:

\begin{lstlisting}
      +---------+
TOS ->|    c    |
      +---------+
      | produit |
      +---------+
\end{lstlisting}

Maintenant l'instruction \TT{iadd} peut ajouter les deux valeurs.

